class MisionImposibleException inherits Exception(message = "Misión no puede comenzar"){}

class Personaje {
	var copas
	
	method copas() = copas
	
	method recibirCopas(cantidad) {
		copas += cantidad
	}
}


class Arquero inherits Personaje{
	var agilidad
	var rango
	
	method destreza() = agilidad * rango
	
	method tieneEstrategia() = rango > 100
}

class Ballestero inherits Arquero{
	override method destreza() = super() * 2
}

class Guerrera inherits Personaje{
	var fuerza
	var property tieneEstrategia
	
	method destreza() = fuerza *1.5 
}

class Mision{
	var modoPremiacion = new Boost(multiplicador = 2)

	method puedeSerSuperada() = self.destrezaSuficiente() || self.estrategiaSuficiente()
	
	method destrezaSuficiente() 
	method estrategiaSuficiente() 

	method repartirCopas() {
		self.validar() 
		self.premiar(self.cantidadDeCopas()* if(self.puedeSerSuperada()) 1 else -1)
//		if (self.puedeSerSuperada())
//			self.premiar(self.cantidadDeCopas())
//		else
//			self.premiar(-self.cantidadDeCopas())
	}
	
	method validar() {
		if (self.noPuedeComenzar())
			throw new MisionImposibleException()
	}
	
	method premiar(cantidad) 
	method noPuedeComenzar()
	
	method cantidadDeCopas() =	modoPremiacion.cantidadCopas(self)
}

class MisionIndividual inherits Mision{
	var personaje
	var dificultad
	
	override method premiar(cantidad) {
		personaje.recibirCopas(cantidad)
	}
		
	method copasBasicas() = dificultad * 2
	
	method cantParticipantes() = 1
	
	override method destrezaSuficiente() = personaje.destreza() > dificultad
	
	override method estrategiaSuficiente() = personaje.tieneEstrategia()
	
	override method noPuedeComenzar() = personaje.copas() < 10
}

class MisionEquipo inherits Mision{
	const personajes = []

	override method premiar(cantidad) {
		personajes.forEach{p => p.darCopas(cantidad)}
	}

	method copasBasicas() = 50 / self.cantParticipantes()
	
	override method estrategiaSuficiente() 
		= self.estrategiaTotal() > self.cantParticipantes() /2
		
	method estrategiaTotal() =
			personajes.count{p=> p.tieneEstrategia()}

	method cantParticipantes() = personajes.size()
		
	override method destrezaSuficiente() = personajes.all{p=>p.destreza() > 400}

	override method noPuedeComenzar() = personajes.sum{p=> p.copas()} < 60
}

object comun {
	method cantidadCopas(mision) = mision.copasBasicas()
}

class Boost {
	var multiplicador 
	method cantidadCopas(mision) = multiplicador * mision.copasBasicas()  
}

object bonus {
	method cantidadCopas(mision) = mision.cantParticipantes() + mision.copasBasicas()
}

/* Pdep Brawls
La empresa Superchet nos ha contratado para desarrollar la lógica de su nuevo juego Pdep Brawls en el lenguaje Wollok. Cualquier coincidencia con juegos existentes es mera casualidad.... ¡El juego es completamente diferente!

En este juego se pueden tener varios personajes con los cuales participar de distintas misiones. Cada personaje tiene una cierta cantidad de copas ganadas y al realizar una misión puede pasar que los personajes ganen o pierdan copas.

Estos son los personajes que existen en este juego, pero puede haber más a futuro:

Arqueros: la destreza de los arqueros se calcula como su agilidad por su rango, y tienen estrategia cuando su rango supera los 100 metros.
Guerreras: hay guerreras que tienen estrategia y guerreras que no. Además, su destreza es 50% más que su fuerza.
Ballesteros: los ballesteros son arqueros entrenados cuya destreza es el doble que la de un arquero normal.
En toda misión, hay copas que están en juego y estas se suman o se restan de los personajes que participan en la misión dependiendo de si la misión puede ser superada o no.

Existen las misiones individuales y las misiones por equipo. En las misiones individuales, las copas que están en juego son el doble de la dificultad de la misión, y puede ser superada cuando el personaje tiene estrategia o bien cuando su destreza supera la dificultad de la misión.

En las misiones por equipo las copas que están en juego son 50 dividido la cantidad de personajes que participan, y pueden ser superadas cuando más de la mitad de los participantes tienen estrategia, o bien, cada uno tiene una destreza mayor a 400.

Se pide codificación para resolver lo siguiente:

Saber si una misión puede ser superada.
Realizar una misión, repartiendo las copas (sumando o restando) de acuerdo a lo establecido. Tener en cuenta que no debe ser posible realizar misiones individuales cuyo participante tenga menos de 10 copas, ni misiones por equipo con un total de copas entre los participantes menor a 60, en ambos casos debe ocurrir el error “Misión no puede comenzar”. Por ejemplo, al realizar una misión por equipo con 5 participantes, cada uno recibe 10 copas (si es que puede ser superada) y si no puede ser superada a cada uno se le restan 10 copas. Pero si la misión no puede realizarse, no ocurre ninguna de las dos cosas y en su lugar salta el error “Misión no puede comenzar”.
El tiempo pasa y nos piden agregar además de las misiones comunes, las misiones Boost y las misiones Bonus. Cada una de las misiones Boost tiene un multiplicador que multiplica la cantidad de copas en juego, mientras que las misiones Bonus otorgan 1 copa más por cada participante de la misión. Tener en cuenta que puede haber misiones comunes, Boost y Bonus tanto por equipo como individuales.
Escribir los tests necesarios para probar el correcto funcionamiento del código que prueba que una misión individual fue superada. Se espera un test bien nombrado por cada clase de equivalencia.
¿En qué lugar de tu solución aparece el polimorfismo? Justificar marcando objetos polimórficos y quién se aprovecha del polimorfismo.
Realizar el diagrama estático de toda la solución. No hace falta poner getters ni setters */
